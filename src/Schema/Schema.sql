-- ================================================================ --
-- Author:			<Carlos Herrera Verdugo>	    --
-- Proyecto:		<Certificación BDOO>                        --
-- Create date: 	<31-10-2017>                                --
-- Description:		<Schema Certificación BDOO>                 --
-- Profesor:		<Claudio Diaz Pacheco>                      --
-- ================================================================ --

-------------------------------------------------------------------------------
---------------------------------USUARIO CERTI---------------------------------
-------------------------------------------------------------------------------

CREATE USER CERTI IDENTIFIED BY 12345;

GRANT CONNECT TO CERTI;
GRANT RESOURCE TO CERTI;
GRANT CREATE ANY TYPE TO CERTI; 
GRANT UNDER ANY TYPE TO CERTI; 
GRANT EXECUTE ANY TYPE TO CERTI;

-------------------------------------------------------------------------------
--------------------------------CREACION OBJETOS-------------------------------
-------------------------------------------------------------------------------

CREATE OR REPLACE TYPE T_PERSONA AS OBJECT(
RUT  VARCHAR(15),
NOMBRE VARCHAR(15),
APELLIDO VARCHAR(15),
TELEFONO VARCHAR(15),
ESTADO NUMBER,
MEMBER FUNCTION FULLNAME RETURN VARCHAR) NOT FINAL;
/

CREATE OR REPLACE TYPE T_CLIENTE UNDER T_PERSONA(
MEMBER FUNCTION TOTALFACTURASMES(M NUMBER) RETURN NUMBER);
/

CREATE OR REPLACE TYPE T_JEFEDEPTO UNDER T_PERSONA(
NOMBREDEPTO VARCHAR(15),
SUELDO NUMBER,
MEMBER FUNCTION NUMEROPERSONAS RETURN NUMBER);
/

CREATE OR REPLACE TYPE T_VENDEDOR UNDER T_PERSONA(
FECHA_INGRESO DATE,
SUELDO_BASE NUMBER,
JEFE REF T_JEFEDEPTO,
MEMBER FUNCTION COMISIONMES(M NUMBER) RETURN NUMBER,
MEMBER FUNCTION SUELDO_MES(M NUMBER) RETURN NUMBER);
/

CREATE OR REPLACE TYPE T_FACTURA AS OBJECT(
NUMERO NUMBER,
FECHA DATE,
CONTACTO REF T_CLIENTE,
VENDIDOPOR REF T_VENDEDOR,
MEMBER FUNCTION TOTAL RETURN NUMBER);
/

CREATE OR REPLACE TYPE T_PRODUCTO AS OBJECT(
CODIGO NUMBER,
DESCRIPCION VARCHAR(15),
PRECIO NUMBER,
STOCK NUMBER,
STOCK_CRITICO NUMBER,
ESTADO NUMBER,
MEMBER FUNCTION TOTALVENDIDOMES(M NUMBER) RETURN NUMBER,
MEMBER FUNCTION TOTALVENDIDOYEAR(Y NUMBER) RETURN NUMBER);
/

CREATE OR REPLACE TYPE T_DETALLEFACTURA AS OBJECT(
DOCUMENTO REF T_FACTURA,
ITEM REF T_PRODUCTO,
CANTIDAD NUMBER,
PRECIO_VENTA NUMBER,
MEMBER FUNCTION TOTAL RETURN NUMBER);
/

-------------------------------------------------------------------------------
--------------------------------CREACION TABLAS--------------------------------
-------------------------------------------------------------------------------

CREATE TABLE CLIENTE OF T_CLIENTE (RUT PRIMARY KEY)
OBJECT IDENTIFIER IS PRIMARY KEY;

CREATE TABLE JEFEDEPTO OF T_JEFEDEPTO (RUT PRIMARY KEY)
OBJECT IDENTIFIER IS PRIMARY KEY;

CREATE TABLE VENDEDOR OF T_VENDEDOR (RUT PRIMARY KEY,
SCOPE FOR (JEFE) IS JEFEDEPTO)
OBJECT IDENTIFIER IS PRIMARY KEY;


CREATE TABLE FACTURA OF T_FACTURA (NUMERO PRIMARY KEY,
SCOPE FOR (CONTACTO) IS CLIENTE,
SCOPE FOR (VENDIDOPOR) IS VENDEDOR)
OBJECT IDENTIFIER IS PRIMARY KEY;

CREATE TABLE PRODUCTO OF T_PRODUCTO (CODIGO PRIMARY KEY)
OBJECT IDENTIFIER IS PRIMARY KEY;

CREATE TABLE DETALLEFACTURA OF T_DETALLEFACTURA (
SCOPE FOR (DOCUMENTO) IS FACTURA,
SCOPE FOR (ITEM) IS PRODUCTO);

-------------------------------------------------------------------------------
--------------------------------CREACION BODIES--------------------------------
-------------------------------------------------------------------------------

CREATE OR REPLACE TYPE BODY T_PERSONA IS
MEMBER FUNCTION FULLNAME RETURN VARCHAR IS
BEGIN
RETURN APELLIDO || ', ' || NOMBRE;
END;
END;
/

CREATE OR REPLACE TYPE BODY T_CLIENTE IS
MEMBER FUNCTION TOTALFACTURASMES (M NUMBER) RETURN NUMBER IS
VALOR NUMBER;
BEGIN
SELECT SUM(F.TOTAL()) INTO VALOR FROM FACTURA F WHERE M=EXTRACT(MONTH FROM F.FECHA) 
AND F.CONTACTO.RUT=SELF.RUT;
IF VALOR IS NULL THEN
VALOR := 0;
END IF;
RETURN VALOR;
END;
END;
/

CREATE OR REPLACE TYPE BODY T_VENDEDOR IS
MEMBER FUNCTION COMISIONMES (M NUMBER) RETURN NUMBER IS
VALOR NUMBER;
BEGIN
SELECT (SUM(F.TOTAL())*0.20) AS COMI INTO VALOR FROM FACTURA F 
WHERE M=EXTRACT(MONTH FROM F.FECHA) AND SELF.RUT=F.VENDIDOPOR.RUT;
IF VALOR IS NULL THEN
VALOR := 0;
END IF;
RETURN VALOR;
END;
MEMBER FUNCTION SUELDO_MES (M NUMBER) RETURN NUMBER IS
VALOR2 NUMBER;
BEGIN
SELECT (V.COMISIONMES(M)+SUELDO_BASE) AS SUELD INTO VALOR2 FROM VENDEDOR V 
WHERE V.RUT=SELF.RUT;
IF VALOR2 IS NULL THEN
VALOR2 := 0;
END IF;
RETURN VALOR2;
END;
END;
/

CREATE OR REPLACE TYPE BODY T_JEFEDEPTO IS
MEMBER FUNCTION NUMEROPERSONAS RETURN NUMBER IS
VALOR NUMBER;
BEGIN
SELECT COUNT(1) INTO VALOR FROM VENDEDOR V WHERE SELF.RUT=V.JEFE.RUT;
RETURN VALOR;
END;
END;
/

CREATE OR REPLACE TYPE BODY T_FACTURA IS
MEMBER FUNCTION TOTAL RETURN NUMBER IS
VALOR NUMBER;
BEGIN
SELECT SUM(DF.TOTAL()) INTO VALOR FROM DETALLEFACTURA DF 
WHERE SELF.NUMERO=DF.DOCUMENTO.NUMERO;
IF VALOR IS NULL THEN
VALOR := 0;
END IF;
RETURN VALOR;
END;
END;
/

CREATE OR REPLACE TYPE BODY T_PRODUCTO IS
MEMBER FUNCTION TOTALVENDIDOMES (M NUMBER) RETURN NUMBER IS
VALOR NUMBER;
BEGIN
SELECT SUM(DF.CANTIDAD) INTO VALOR FROM DETALLEFACTURA DF 
WHERE M=EXTRACT(MONTH FROM DF.DOCUMENTO.FECHA) AND DF.ITEM.CODIGO=SELF.CODIGO;
IF VALOR IS NULL THEN
VALOR := 0;
END IF;
RETURN VALOR;
END;
MEMBER FUNCTION TOTALVENDIDOYEAR (Y NUMBER) RETURN NUMBER IS
VALOR2 NUMBER;
BEGIN
SELECT SUM(DF.CANTIDAD) INTO VALOR2 FROM DETALLEFACTURA DF 
WHERE Y=EXTRACT(YEAR FROM DF.DOCUMENTO.FECHA) AND DF.ITEM.CODIGO=SELF.CODIGO;
IF VALOR2 IS NULL THEN
VALOR2 := 0;
END IF;
RETURN VALOR2;
END;
END;
/

CREATE OR REPLACE TYPE BODY T_DETALLEFACTURA IS
MEMBER FUNCTION TOTAL RETURN NUMBER IS
BEGIN
RETURN CANTIDAD*PRECIO_VENTA;
END;
END;
/